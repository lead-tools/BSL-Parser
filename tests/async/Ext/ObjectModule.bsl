Перем КонтекстЯдра;
Перем Утверждения;
перем Ожидаем;
//{ основная процедура для юнит-тестирования xUnitFor1C
Процедура Инициализация(КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	Утверждения = КонтекстЯдра.Плагин("БазовыеУтверждения");
	Ожидаем = КонтекстЯдра.Плагин("УтвержденияBDD");
КонецПроцедуры

Процедура ЗаполнитьНаборТестов(НаборТестов, КонтекстЯдраПараметр) Экспорт
	КонтекстЯдра = КонтекстЯдраПараметр;
	НаборТестов.Добавить("ТестДолжен_РазобратьКодСАсинх");
	НаборТестов.Добавить("ТестДолжен_РазобратьКодСИдентификаторомЖдать");
	НаборТестов.Добавить("ТестДолжен_РазобратьКодПроцедуры");
	
КонецПроцедуры

//}

//{ Блок юнит-тестов


Функция ВернутьОбработку(Имя)
	
	Файл = новый Файл(ЭтотОбъект.ИспользуемоеИмяФайла); 
	ИмяОбработки = файл.Путь+"..\"+Имя+?(СтрЗаканчиваетсяНа(Имя,".epf"),"",".epf");
	УбраннаяЗащита = новый ОписаниеЗащитыОтОпасныхДействий;
	УбраннаяЗащита.ПредупреждатьОбОпасныхДействиях = Ложь;
	Возврат ВнешниеОбработки.Создать(ИмяОбработки,Истина,УбраннаяЗащита);
	
	
КонецФункции

Процедура ПередЗапускомТеста() Экспорт
КонецПроцедуры

Процедура ПослеЗапускаТеста() Экспорт
КонецПроцедуры


процедура ТестДолжен_РазобратьКодСАсинх() экспорт
	
	Парсер = ВернутьОбработку("ПарсерВстроенногоЯзыка");
	
	ТекстМодуля = "&НаКлиенте
	|   Асинх Процедура НашаПроцедура()
	|           х = Ждать ЧтоТоЖдем;
	|   КонецПроцедуры";
	
	Модуль = Парсер.Разобрать(ТекстМодуля, Неопределено);
	
	Ожидаем.Что(Модуль.Объявления[0].Сигнатура.Асинх,"Корректно прочли определение").ЭтоИстина();
	Ожидаем.Что(Модуль.Объявления[0].Операторы[0].ПравыйОперанд.Тип,"Прочли обещание").Равно("ОператорВызоваОбещания");
	
КонецПроцедуры


процедура ТестДолжен_РазобратьКодСИдентификаторомЖдать() экспорт
	
	Парсер = ВернутьОбработку("ПарсерВстроенногоЯзыка");
	
	ТекстМодуля = "Процедура НашаПроцедура()
	|           Ждать = 1;
	|   КонецПроцедуры";
	
	Модуль = Парсер.Разобрать(ТекстМодуля, Неопределено);
	
	Ожидаем.Что(Модуль.Объявления[0].Операторы[0].ЛевыйОперанд.Голова.Имя,"Корректно прочли Ждать как идентификатор").Равно("Ждать");
	
КонецПроцедуры


процедура ТестДолжен_РазобратьКодПроцедуры() экспорт
	
	Парсер = ВернутьОбработку("ПарсерВстроенногоЯзыка");
	
	ТекстМодуля = "&НаКлиенте
	|Асинх Процедура Демо_НайтиФайлыАсинх()
	|  ПутьКПапкеСФайлами = ""C:\Program Files\1cv8\conf"";
	|  Обещание = НайтиФайлыАсинх(ПутьКПапкеСФайлами, ""*.*"", Ложь);
	|  Результат = Ждать Обещание;
	|КонецПроцедуры";
	
	Модуль = Парсер.Разобрать(ТекстМодуля, Неопределено);
	
	Ожидаем.Что(Модуль.Объявления[0].Операторы[2].ПравыйОперанд.Тип,"Разобрали код корректно").Равно("ОператорВызоваОбещания");
	
КонецПроцедуры

